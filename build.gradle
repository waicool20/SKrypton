/*
 * The MIT License (MIT)
 *
 * Copyright (c) SKrypton by waicool20
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 */

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

buildscript {
    ext.kotlin_version = '1.1.51'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.4'
    id "com.jfrog.artifactory" version "4.5.2"
}

allprojects {
    apply plugin: 'idea'
    idea {
        module {
            inheritOutputDirs = false
            outputDir = file('build/classes/main')
            testOutputDir = file('build/classes/test')
        }
    }
}

apply from: 'common.gradle'
apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'

group 'com.waicool20.skrypton'
version '1.0.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'

    /* sikulix */
    compile(group: "com.sikulix", name: "sikulixapi", version: "1.1.2-SNAPSHOT") {
        exclude group: "com.sikulix"
        exclude module: 'jadb'
    }

    /* Test */
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

shadowJar {
    classifier ""
    archiveName "${rootProject.name}.jar"
    include("com/waicool20/skrypton/**")
    include("META-INF/**")
}

task testJar(type: ShadowJar) {
    from sourceSets.main.output
    from sourceSets.test.output
    configurations = [project.configurations.testRuntime]
    archiveName 'skrypton-test.jar'
    manifest {
        attributes 'Main-Class': 'com.waicool20.skrypton.MainKt'
    }
}

task srcJar(type: ShadowJar) {
    classifier "sources"
    archiveName "${rootProject.name}-sources.jar"
    from sourceSets.main.allSource
}

jar.enabled = false
build.finalizedBy shadowJar, ':skrypton-native:nativeJarRelease'

task buildAll {
    group 'build'
    dependsOn 'build', 'testJar', ':skrypton-native:nativeJarDebug'
}

//<editor-fold desc="Publishing">

if (publishApiKey == null || publishApiKey.isEmpty()) {
    tasks.findAll { it.group == 'publishing' }.each {
        it.enabled = false
    }
}

publishing {
    publications {
        SKryptonAPI(MavenPublication) {
            artifact shadowJar
            artifact srcJar
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name rootProject.name
                    description "A browser automation library that uses SikuliX and doesn't grab grab your mouse"
                    url "https://github.com/waicool20/SKrypton"
                    packaging "jar"
                    licenses {
                        license {
                            name "The MIT License (MIT)"
                            url "http://opensource.org/licenses/MIT"
                        }
                    }
                    developers {
                        developer {
                            name "waicool20"
                            email "waicool20@gmail.com"
                            organization "waicool20"
                            organizationUrl "https://www.waicool20.com"
                        }
                    }
                    scm {
                        url "https://github.com/waicool20/SKrypton"
                        connection "scm:git:git://github.com/waicool20/SKrypton.git"
                        developerConnection "scm:git:ssh://github.com/waicool20/SKrypton.git"
                    }
                }
                def deps = asNode().appendNode('dependencies')
                configurations.compile.dependencies.each {
                    if((it.group != null) && (it.name != null) && (it.version != null)) {
                        def dep = deps.appendNode('dependency')
                        dep.appendNode('groupId', it.group)
                        dep.appendNode('artifactId', it.name)
                        dep.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

artifactory {
    contextUrl = 'https://oss.jfrog.org/artifactory/'
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = "waicool20"
            password = publishApiKey
            maven = true
        }
        defaults {
            publications('SKryptonAPI')
            publishArtifacts = true
            properties = ['qa.level': 'basic', 'dev.team': 'core']
            publishPom = true
        }
    }
    resolve {
        repoKey = 'jcenter'
    }
}

artifactoryPublish.dependsOn 'srcJar'
publish.dependsOn 'artifactoryPublish'

//</editor-fold>
