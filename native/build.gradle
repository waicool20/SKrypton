import java.nio.file.Files
import java.nio.file.Paths

String cmakeBuildDir = 'cmake-build-debug'

apply from: '../common.gradle'
apply plugin: 'java'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

// Disable all tasks, stops gradle from generating files in build directory
for (task in project.tasks) {
    task.enabled = false
}

task refreshCMake(type: Exec) {
    def file = Paths.get("$projectDir").resolve(cmakeBuildDir)
    if (Files.notExists(file)) Files.createDirectory(file)
    workingDir cmakeBuildDir
    commandLine 'cmake'
    String generator
    if (isWindows) {
        generator = "Visual Studio 15 2017"
        if (is64Bit) generator += " Win64"
    } else {
        generator = "Unix Makefiles"
    }
    args += ['-G', generator, '../']
}

task makeNativeLibraries(type: Exec) {
    dependsOn refreshCMake
    workingDir cmakeBuildDir
    commandLine 'cmake'
    args += ['--build', './']
    if (isUnix) {
        int threads = Runtime.runtime.availableProcessors() * 2
        args += ['--', "-j$threads"]
    }
}

task copyKotlinStdLib(type: Copy) {
    into "build/kstdlib"
    include("kotlin/jvm/functions/**/*")
    include("kotlin/Function.class")
    configurations.runtime.files.each {
        if (it.name.contains("kotlin")) {
            from zipTree(it)
        }
    }
}
